;; TODO:
;;   - compare to SBT/Auction official code example
;;   - write schemas for inbound msg requests
;;   - update TEP





;; NFT Signature Smart Contract
;; For details see TEP https://github.com/ton-era/TEPs/blob/master/text/0090-tsc-standard.md.


;; -------------------------- Constants --------------------------

int const::min_gas_amount()       asm "10000000 PUSHINT"; ;; 0.01 TON
int const::min_tons_for_storage() asm "60000000 PUSHINT"; ;; 0.06 TON
int const::return_amount()        asm "10000000 PUSHINT"; ;; 0.01 TON

int exit::wrong_init_address()       asm "901 PUSHINT";
int exit::wrong_signee_address()     asm "902 PUSHINT";
int exit::wrong_item_address()       asm "903 PUSHINT";
int exit::signee_already_approved()  asm "904 PUSHINT";
int exit::owner_already_approved()   asm "905 PUSHINT";
int exit::wrong_provider_address()   asm "906 PUSHINT";
int exit::insufficient_init_value()  asm "907 PUSHINT";
int exit::insufficient_msg_value()   asm "908 PUSHINT";
int exit::already_inited()           asm "909 PUSHINT";
int exit::unknown_operation()        asm "0xffff PUSHINT";


;; -------------------------- Storage --------------------------

;; payload#_
;;    content_code:uint32
;;    content:^Cell = Payload;

;; storage#_
;;    provider_address:MsgAddressInt
;;    item_address:MsgAddressInt
;;    signee_address:MsgAddressInt
;;    time_created:Bool
;;    time_owner_approve:Bool
;;    time_signee_approve:Bool
;;    payload:(Maybe ^Payload) = Storage;


global slice provider_address; 
global slice item_address;
global slice signee_address;
global int   time_created;
global int   time_owner_approve;
global int   time_signee_approve;
global int   has_payload;
global int   payload_code;
global cell  payload;


() load_data() impure {
    slice ds = get_data().begin_parse();

    provider_address = ds~load_msg_addr();
    item_address     = ds~load_msg_addr();
    signee_address   = ds~load_msg_addr();

    if (ds.slice_bits() == 0) {  ;; not initialized yet
        time_created = 0;
        time_owner_approve  = 0;
        time_signee_approve = 0;
        has_payload = 0;

        return ();
    }

    time_created = ds~load_uint(32);
    time_owner_approve  = ds~load_uint(32);
    time_signee_approve = ds~load_uint(32);
    has_payload = (ds.slice_bits() > 0);
    if (has_payload) {
        payload_code = ds~load_uint(32);
        payload = ds~load_ref();
    } else {
        payload_code = 0;
        payload = null();
    }

    return ();
}


() save_data() impure {
    builder ds = begin_cell()
            .store_slice(provider_address)
            .store_slice(item_address)
            .store_slice(signee_address)
            .store_uint(time_created, 32)
            .store_uint(time_owner_approve, 32)
            .store_uint(time_signee_approve, 32);

    if (has_payload) {
      ds = ds.store_uint(payload_code, 32)
             .store_ref(payload);
    }

    set_data(ds.end_cell());
}


;; -------------------------- handlers --------------------------

() check_approved() impure {
    if ((time_owner_approve == 0) | (time_signee_approve == 0)) {
      return ();
    }

    var msg = begin_cell()
          .store_uint(0x10, 6) ;; nobounce
          .store_slice(provider_address)
          .store_coins(0)
          .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
          .store_uint(op::notify_sign(), 32)
          .store_uint(0, 64) 
          .store_slice(item_address)
          .store_slice(signee_address);

    send_raw_message(msg.end_cell(), 1);  ;; paying fees, revert on errors

  return ();
}

() return_item(slice in_msg_body) impure {
    int query_id = in_msg_body~load_uint(64);
    slice item_owner_address = in_msg_body~load_msg_addr();

    var msg = begin_cell()
        .store_uint(0x10, 6) ;; nobounce
        .store_slice(item_address)
        .store_coins(const::return_amount())
        .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
        .store_uint(op::transfer(), 32)
        .store_uint(query_id, 64)
        .store_slice(item_owner_address) ;; owner_address
        .store_uint(0, 2) ;; empty response_address;
        .store_int(0, 1)  ;; empty custom_payload
        .store_coins(0)   ;; forward amount to owner_address
        .store_int(0, 1); ;; empty forward_payload

    send_raw_message(msg.end_cell(), 1);  ;; paying fees, revert on errors

    time_owner_approve = now();
    check_approved();

    save_data();

    return ();
}


() handle::init(int my_balance, int msg_value, slice sender_address, slice in_msg_body) impure {
    throw_unless(exit::wrong_init_address(), equal_slices(sender_address, provider_address));
    throw_if(exit::already_inited(), time_created > 0);
    throw_if(exit::insufficient_init_value(), my_balance + msg_value < const::min_tons_for_storage());

    time_created = now();
    save_data();

    return ();
}


() handle::sign(slice sender_address, slice in_msg_body) impure {
    throw_unless(exit::wrong_signee_address(), equal_slices(sender_address, signee_address));
    throw_if(exit::signee_already_approved(), time_signee_approve > 0);

    has_payload = in_msg_body~load_uint(1);
    if (has_payload) {
        payload_code = in_msg_body~load_uint(32);
        payload = in_msg_body~load_ref();
    }

    int need_notify = in_msg_body~load_uint(1);
    if (need_notify) {
        int query_id = in_msg_body~load_uint(64);

        slice notify_address = in_msg_body~load_msg_addr();
        int notify_amount = in_msg_body~load_coins();
        cell notify_payload = in_msg_body~load_ref();

        var msg = begin_cell()
          .store_uint(0x10, 6) ;; nobounce
          .store_slice(notify_address)
          .store_coins(notify_amount)
          .store_uint(0, 1 + 4 + 4 + 64 + 32 + 1 + 1)
          .store_uint(op::sign(), 32)
          .store_uint(query_id, 64)
          .store_ref(notify_payload);

        send_raw_message(msg.end_cell(), 1);  ;; paying fees, revert on errors
    }

    time_signee_approve = now();
    check_approved();

    save_data();
}


() handle::fix(slice sender_address, slice in_msg_body) impure {
    throw_unless(exit::wrong_provider_address(), equal_slices(sender_address, provider_address));
    return_item(in_msg_body);
    return();
}


() handle::approve(slice sender_address, slice in_msg_body) impure {
    throw_unless(exit::wrong_item_address(), equal_slices(sender_address, item_address));
    throw_if(exit::owner_already_approved(), time_owner_approve > 0);
    return_item(in_msg_body);
    return();
}

;; -------------------------- Internal --------------------------

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    ;; ignore empty messages: accept coins
    if (in_msg_body.slice_empty?()) {
        return ();
    }

    ;; ignore all bounced messages
    slice cs = in_msg_full.begin_parse();
    int flags = cs~load_uint(4);
    if (flags & 1) {
        return ();
    }

    slice sender_address = cs~load_msg_addr();

    load_data();

    ;; initialize contract
    if (time_created == 0) {
        handle::init(my_balance, msg_value, sender_address, in_msg_body);
        return ();
    }

    ;; gas safeguard
    throw_unless(exit::insufficient_msg_value(), msg_value >= const::min_gas_amount());

    int op = in_msg_body~load_uint(32);

    ;; sign - approve by signee
    if (op == op::approve()) {
        handle::sign(sender_address, in_msg_body);
        return();
    }

    ;; approve by item owner
    if (op == op::ownership_assigned()) {
        handle::approve(sender_address, in_msg_body);
        return ();
    }

    ;; fix owner - return item to owner in case of item freeze on this contract
    if (op == op::fix_owner()) {
        handle::fix(sender_address, in_msg_body);
        return ();
    }

    ;; if there was response_destination upon item transfer - ignore it
    if ((op == op::excesses()) & equal_slices(sender_address, item_address)) {
        return ();
    }

    throw(exit::unknown_operation());
}


;; -------------------------- GET Methods --------------------------

(slice, slice, slice, int, int, int, int, cell) get_info() method_id {
  load_data();
  return (
    provider_address,
    item_address,
    signee_address,
    time_created,
    time_owner_approve,
    time_signee_approve,
    payload_code,
    payload
  );
}
